' get initials and notes from All Therapists sheet and put them in the correct
' boxes in the All Schedules sheet

Public Sub getInitialsAndNotes()
    ' declare variables
    Dim cell As Range
    Dim newCell As Range
    
    ' set first initial box cell
    Set newCell = Sheets("All Schedules").Range("$O$1")
    
    For Each cell In Sheets("All Therapists").Range("AllTherapistsInitials")
        ' if cell has initials and at least 1 room number, get initials
        If cell.Value <> "-" And Not IsEmpty(cell) And Not IsEmpty(cell.Offset(0, 4)) Then
            newCell.Value = cell.Value
            If Not IsEmpty(cell.Offset(0, 25)) Then
                newCell.Offset(26, -14).Value = cell.Offset(0, 25).Value
            End If
            ' go to next initial box
            Set newCell = newCell.Offset(33, 0)
        End If
    Next cell
    
End Sub

' match initials from therapist schedule to initials in All Therapists sheet and grab
' room numbers for that therapist

Public Sub getRooms()
    Dim cell As Range
    Dim newCell As Range
    Dim rng As Range
    Dim j As Integer
    Dim k As Integer
       
    ' set newCell to initial value - first initials box
    Set newCell = Sheets("All Schedules").Range("$O$1")
    ' set counter for newCell row offset
    j = 3
    
    ' for each cell in All Therapists initials column, if value of cell (rng)
    ' matches value of newCell, grab the room numbers and put them in the correct
    ' schedule
    For Each rng In Sheets("All Therapists").Range("AllTherapistsInitials")
        If newCell.Value = rng.Value Then
           ' set counter (k) for All Therapists initials cell column numbers
           For k = 4 To 21
            If Not IsEmpty(rng.Offset(0, k).Value) Then
                newCell.Offset(j, -14).Value = rng.Offset(0, k).Value
                ' increment newCell row offset
                j = j + 1
            End If
           Next k
          ' go to the next therapist initials box in schedule
          Set newCell = newCell.Offset(33, 0)
        End If
       ' reset counter for newCell row offset
       j = 3
 
    Next rng
   
End Sub

' read rooms in All Schedules and match to 3W and 8P schedules; populate All Schedules
' for each room
Public Sub getSchedules()
    ' declare variables
    Dim therapistRoom As Range
    Dim room3W As Range
    Dim room8P As Range
    Dim allSchedRooms As Range
    Dim rooms3WSched As Range
    Dim rooms8PSched As Range
    
    ' set variables
    Set allSchedRooms = Sheets("All Schedules").Range("RoomsAllSchedules")
    Set rooms3WSched = Sheets("3W Schedule").Range("Rooms3WSchedule")
    Set rooms8PSched = Sheets("8P Schedule").Range("Rooms8PSchedule")
    
    ' Grab rooms from 3W Schedule
    For Each therapistRoom In allSchedRooms
        For Each room3W In rooms3WSched
            If IsEmpty(therapistRoom.Value) Or therapistRoom.Value > 89 Then
                Exit For
            End If
            
            If therapistRoom.Value = room3W.Value Then
                Range(room3W.Offset(0, 1), room3W.Offset(0, 22)).Copy
                Range(therapistRoom.Offset(0, 4), therapistRoom.Offset(0, 24)).PasteSpecial xlPasteValues
                Exit For
            End If
        Next room3W
    Next therapistRoom
    
    ' Grab rooms from 8P Schedule
    For Each therapistRoom In allSchedRooms
        For Each room8P In rooms8PSched
            If therapistRoom.Value = room8P.Value Then
                Range(room8P.Offset(0, 1), room8P.Offset(0, 22)).Copy
                Range(therapistRoom.Offset(0, 4), therapistRoom.Offset(0, 24)).PasteSpecial xlPasteValues
                Exit For
            End If
        Next room8P
    Next therapistRoom
    
    ' turn cut copy mode off
    Application.CutCopyMode = False
End Sub

' clear contents from All Schedules Sheet
Public Sub clearAllSched()
    Sheets("All Schedules").Range("AllSchedTherapistInitialsBox").Value = ""
    Sheets("All Schedules").Range("RoomsAllSchedules").ClearContents
    Sheets("All Schedules").Range("AllSchedDataGrid").ClearContents
    Sheets("All Schedules").Range("AllSchedNoteAreas").ClearContents
     Sheets("All Schedules").Range("$AD$8:$AD$11").ClearContents
End Sub

' Count number of 3W, 8P & 3P therapists and print the totals in a table
Public Sub countTherapists()
    Dim count3W As Integer
    Dim count8P As Integer
    Dim count3P As Integer
    Dim cell As Range
    
    ' initialize counters
     count3W = 0
     count8P = 0
     count3P = 0
     
     For Each cell In Sheets("All Therapists").Range("AllTherapistsInitials")
        If Not IsEmpty(cell) And cell.Value <> "-" And Not IsEmpty(cell.Offset(0, 4)) Then
            If cell.Offset(0, 1).Value = "3W" Then
                count3W = count3W + 1
            ElseIf cell.Offset(0, 1).Value = "8P" Then
                count8P = count8P + 1
            Else
                count3P = count3P + 1
            End If
        End If
     Next cell

    ' add therapist count to sheet
    Sheets("All Schedules").Range("$AD$8").Value = count3W
    Sheets("All Schedules").Range("$AD$9").Value = count8P
    Sheets("All Schedules").Range("$AD$10").Value = count3P
    Sheets("All Schedules").Range("$AD$11").Value = count3W + count8P + count3P
    
End Sub


' clear cells and populate schedules; scroll back to top of page
Public Sub createSchedules()
    ' clear current schedules and therapist counts
    Call clearAllSched
    ' get initials
    Call getInitialsAndNotes
    ' get rooms
    Call getRooms
    ' get 3W and 8P schedules
    Call getSchedules
    ' count therapists
    Call countTherapists
    ' go back to top of sheet
    ActiveWindow.ScrollRow = 1
    
End Sub
